import java.util.zip.ZipFile

plugins {
    id "com.github.davidmc24.gradle.plugin.avro" version "1.2.1"
}

repositories {
    mavenCentral()
}
configurations {
    additionalSchema
}
dependencies {
    implementation "org.apache.avro:avro:1.10.1"
    additionalSchema project(":schema")
}

generateAvroJava {
    dependsOn configurations.additionalSchema
    source {
        // As of Gradle 7.2, Using zipTree within source appears to disable build caching
        configurations.additionalSchema.collect { zipTree(it) }
    }
}

def configureJar = tasks.register("configureJar") {
    it.doLast {
        // Exclude classes that are already in schema.jar from this jar
        tasks.jar.exclude(
            configurations.additionalSchema
                .findAll { it.name.endsWith("jar") }
                .collect { File file ->
                    new ZipFile(file).entries()
                        .findAll { it.name.endsWith(".class") }
                        .collect { it.name }
                }
                .flatten()
        )
    }
    // otherwise the jars of dependent projects might not have been built
    // TODO is there a way to copy the dependencies of the jar task? classes is not part of tasks.jar.dependsOn
    it.dependsOn(tasks.classes)
}

tasks.named("jar") {
    it.dependsOn(configureJar)
}
