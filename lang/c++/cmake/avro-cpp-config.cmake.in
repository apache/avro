#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
# This config sets the following variables in your project::
#
#   avro-cpp_FOUND - true if avro-cpp found on the system
#   avro-cpp_VERSION - version of the found avro-cpp
#
# This config sets the following targets in your project::
#
#   avro-cpp::avrocpp_shared
#   avro-cpp::avrocpp_static

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

if(DEFINED CMAKE_MODULE_PATH)
  set(AVRO_CMAKE_MODULE_PATH_OLD ${CMAKE_MODULE_PATH})
else()
  unset(AVRO_CMAKE_MODULE_PATH_OLD)
endif()
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

find_dependency(ZLIB REQUIRED)
find_dependency(fmt REQUIRED)
if(@Snappy_FOUND@)
    find_dependency(Snappy REQUIRED)
endif()
if(@Boost_FOUND@)
    find_dependency(Boost 1.70 REQUIRED)
endif()
if(@zstd_FOUND@)
    find_dependency(zstd REQUIRED)
endif()

if(DEFINED AVRO_CMAKE_MODULE_PATH_OLD)
  set(CMAKE_MODULE_PATH ${AVRO_CMAKE_MODULE_PATH_OLD})
  unset(AVRO_CMAKE_MODULE_PATH_OLD)
else()
    unset(CMAKE_MODULE_PATH)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/avro-cpp-targets.cmake")

if(@AVRO_BUILD_STATIC@)
  add_library(avro-cpp::avrocpp_static ALIAS avro-cpp::avrocpp_s)
endif()
if(@AVRO_BUILD_SHARED@)
  add_library(avro-cpp::avrocpp_shared ALIAS avro-cpp::avrocpp)
endif()

check_required_components(avro-cpp)
